#!/bin/zsh

if [[ $OSTYPE != 'darwin'* ]]; then
  echo "This command is only available in MacOS (Darwin) environment."
  return
fi

_ymt_volume_usage() {
  cat <<EOF
volume is a command to change the volume of your MacOS.

Usage:
    volume 50        set output volume to 50 (available:0-100)
    volume status    print current volume status
    volume mute      mute the volume
    volume unmute    unmute the volume
    volume half      set output volume to 50
    volume max       set output volume to 100

Options:
    --help, -h       print help

Dependencies:
    MacOS
EOF
}

_ymt_volume_status() {
  local settings=$(osascript -e 'get volume settings')
  echo $settings | awk '{ print $2 }' | sed -e 's/,//'
  echo $settings | awk '{ print $8 }'
}

case ${1} in
  --help|-h)
    _ymt_volume_usage
  ;;

  status)
    _ymt_volume_status
  ;;

  mute)
    osascript -e 'set volume output muted true'
    _ymt_volume_status
  ;;

  unmute)
    osascript -e 'set volume output muted false'
    _ymt_volume_status
  ;;

  half)
    osascript -e "set volume output volume 50"
    _ymt_volume_status
  ;;

  max)
    osascript -e "set volume output volume 100"
    _ymt_volume_status
  ;;

  *)
    if [[ $1 =~ ^(100|[0-9]{1,2})$ ]]; then
      osascript -e "set volume output volume $1"
      _ymt_volume_status
    else
      echo "Plead specify a number between 0 and 100."
    fi
  ;;
esac

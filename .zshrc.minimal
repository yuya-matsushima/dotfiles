[ -n "$ZSH_PROFILE" ] && zmodload zsh/zprof && zprof
export LANG=ja_JP.UTF-8
case ${UID} in
0)
    LANG=C
    ;;
esac

local zshrc_path=$HOME/.zshrc.minimal
[ -L $zshrc_path ] && local zshrc_path=$(readlink $HOME/.zshrc.minimal)

if [ ! -e $HOME/.zshrc.minimal.zwc ] || [ $zshrc_path -nt $HOME/.zshrc.minimal.zwc ]; then
  zcompile $HOME/.zshrc.minimal
  [ -n "$DEBUG_ZSHRC" ] && echo "compiled the \$HOME/.zshrc.minimal file.: .zshrc.minimal is changed"
fi

[ -f $HOME/.zshenv ] && source $HOME/.zshenv

autoload colors && colors

case ${UID} in
0)
    PROMPT="%{${fg[cyan]}%}$(echo ${HOST%%.*} | tr '[a-z]' '[A-Z]') %B%{${fg[red]}%}%30<~<%/%%%{${reset_color}%}%b "
    PROMPT2="%B%{${fg[red]}%}%_#%{${reset_color}%}%b "
    SPROMPT="%B%{${fg[white]}%}%r is correct? [n,y,a,e]:%{${reset_color}%}%b "
    ;;
*)
    PROMPT="%{${fg[cyan]}%}%30<~<%/%%%{${reset_color}%} "
    PROMPT2="%{${fg[red]}%}%_%%%{${reset_color}%} "
    SPROMPT="%{${fg[red]}%}%r is correct? [n,y,a,e]:%{${reset_color}%} "
    [ -n "${REMOTEHOST}${SSH_CONNECTION}" ] &&
        PROMPT="%{${fg[red]}%}$(echo ${HOST%%.*} | tr '[a-z]' '[A-Z]') ${PROMPT}"
    ;;
esac

setopt auto_cd
setopt auto_pushd
setopt correct
setopt list_packed
setopt noautoremoveslash
setopt nolistbeep
setopt nobeep
setopt extendedglob

bindkey -d
bindkey -v
bindkey '\e\e' vi-cmd-mode
bindkey '^a' beginning-of-line
bindkey '^e' end-of-line

autoload history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end
bindkey "^p" history-beginning-search-backward-end
bindkey "^n" history-beginning-search-forward-end
bindkey "\e[Z" reverse-menu-complete

# history
HISTFILE=${HOME}/.zsh_history
HISTSIZE=250000
SAVEHIST=250000
setopt hist_ignore_dups
setopt share_history

autoload bashcompinit && bashcompinit
autoload -Uz compinit && compinit -u
setopt complete_aliases
autoload zed
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

## alias
setopt complete_aliases     # aliased ls needs if file/dir completions work

case "${OSTYPE}" in
freebsd*|darwin*)
    if (( $+commands[eza] )); then
      alias ls="eza --time-style=long-iso"
      alias la="eza -a --time-style=long-iso"
      alias ll="eza -l --time-style=long-iso"
      alias lt="eza --sort=modified --time-style=long-iso"
    else
      alias ls="ls -G -w"
      alias la="ls -a"
      alias ll="ls -l"
      alias lt="ls -t"
    fi
    ;;
linux*)
    if (( $+commands[eza] )); then
      alias ls="eza --time-style=long-iso"
      alias la="eza -a --time-style=long-iso"
      alias ll="eza -l --time-style=long-iso"
      alias lt="eza --sort=modified --time-style=long-iso"
    else
      alias ls="ls --color"
      alias la="ls -a"
      alias ll="ls -l"
      alias lt="ls -t"
    fi
    ;;
esac
alias l="ls"
alias j="jobs -l"
if (( $+commands[erd] )); then
  alias tree='erd --color=auto'
  alias treeless='erd --color=force | less -R'
fi
(( $+commands[rg] )) && alias rgless='rg --pcre2 --pretty --context 2 --no-heading --color=always'
(( $+commands[colordiff] )) && alias diff="colordiff -u"
(( $+commands[delta] )) && alias gitdiff="git diff --no-index"
if (( $+commands[asdf] )); then
  local asdf_path="$(brew --prefix asdf 2>/dev/null)/libexec/asdf.sh"
  [[ -f "$asdf_path" ]] && . "$asdf_path"
fi
(( $+commands[awsume] )) && alias awsume="source awsume"
(( $+commands[direnv] )) && eval "$(direnv hook zsh)"
# zoxide: 非対話シェル (Claude Code 等) での __zoxide_z エラーを回避するため対話シェルでのみ初期化
[[ $- == *i* ]] && (( $+commands[zoxide] )) && eval "$(zoxide init zsh --cmd cd)"
# Use GNU grep if available (for personal use only)
(( $+commands[ggrep] )) && alias ggrep="$HOMEBREW_PREFIX/bin/ggrep"
if (( $+commands[git] )); then
  alias root_path='git rev-parse --show-toplevel 2>/dev/null || echo .'
  alias root='cd $(root_path)'
fi
if (( $+commands[go] )); then
  export GOPATH=$(go env GOPATH)
  export GOBIN=${GOPATH}/bin
  export ASDF_GOLANG_MOD_VERSION_ENABLED=true
fi
(( $+commands[kubectl] )) && source <(kubectl completion zsh 2>/dev/null) 2>/dev/null
(( $+commands[codex] )) && source <(codex completion zsh 2>/dev/null) 2>/dev/null
(( $+commands[qr] )) && alias qr="qrencode -t UTF8"
(( $+commands[uv] )) && eval "$(uv generate-shell-completion zsh 2>/dev/null)" 2>/dev/null
if (( $+commands[nvim] )); then
  alias ni="nvim"
  alias vi="nvim"
fi
# Use local claude installation if available
[[ -x "$HOME/.claude/local/claude" ]] && alias claude="$HOME/.claude/local/claude"
# tinyvim: vim with minimal configuration
if [[ -f "$HOME/.vimrc.minimal" || -L "$HOME/.vimrc.minimal" ]]; then
  alias tinyvim='vim -u "$HOME/.vimrc.minimal"'
fi
# tealdeer: tldr client
(( $+commands[tldr] )) && alias tldr="tldr --language=en"
if (( $+commands[fzf] )); then
  source <(fzf --zsh 2>/dev/null) 2>/dev/null || true
fi
(( $+commands[lazygit] )) && alias lg='lazygit'

case "${TERM}" in
screen)
    TERM=xterm
    ;;
screen-256color)
    TERM=xterm-256color
    ;;
esac

case "${TERM}" in
xterm|xterm-color|xterm-256color)
    export LSCOLORS=exfxcxdxbxegedabagacad
    export LS_COLORS='di=34:ln=35:so=32:pi=33:ex=31:bd=46;34:cd=43;34:su=41;30:sg=46;30:tw=42;30:ow=43;30'
    zstyle ':completion:*' list-colors 'di=34' 'ln=35' 'so=32' 'ex=31' 'bd=46;34' 'cd=43;34'
    ;;
esac

REPORTTIME=3

[ -f $HOME/.zsh/asdf_completion.zsh ] && source $HOME/.zsh/asdf_completion.zsh
(( $+commands[less] )) && source $HOME/.zsh/config/less.zsh

[ -f $HOME/.zshrc_local ] && source $HOME/.zshrc_local

[ -n "$ZSH_PROFILE" ] && zprof | less
